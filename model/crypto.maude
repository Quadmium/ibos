--- Shared sorts between modules
mod CRYPTO_ANCESTOR is
pr NAT .
pr STRING .

--- This corresponds to message contents, not who to where
sort Payload .

sort SymKey PrivKey PubKey .

sort Nonce .
subsort Nonce < Payload .
op nonce : Nat -> Nonce [ctor] .

sort DecryptionResult .
sort Error .
subsorts Error Payload < DecryptionResult .

op error : String -> Error [ctor] .
endm

--- Symmetric encryption
mod SYMMETRIC is
pr CRYPTO_ANCESTOR .

--- Nat could be counter or nonce
op symKey : Nat -> SymKey [ctor] .

op enc : Payload SymKey -> Payload [ctor] .

op dec : Payload SymKey -> DecryptionResult [ctor] .

vars P : Payload .
vars K K' : SymKey .

eq dec(enc(P, K), K) = P .
eq dec(P, K) = error("Mismatched symmetric key OR unencrypted payload") [owise] .
endm

--- Asymmetric encryption
mod ASYMMETRIC is
pr CRYPTO_ANCESTOR .

op privKey : Nat -> PrivKey [ctor] .
op pubKey : Nat -> PubKey [ctor] .

--- Standard encrypt, decrypt, sign, verify
op enc : Payload PubKey -> Payload [ctor] .
op dec : Payload PrivKey -> DecryptionResult [ctor] .

op sign : Payload PrivKey -> Payload [ctor] .
op verify : Payload PubKey -> DecryptionResult [ctor] .

vars N : Nat .
vars P : Payload .
vars KPriv : PrivKey .
vars KPub : PubKey .

--- Flip is a two way map between PubKey and PrivKey
--- By defining your own "flip", you can extend the model to support any kind of priv/pub key
op flip : PubKey -> PrivKey .
op flip : PrivKey -> PubKey .
eq flip(pubKey(N)) = privKey(N) .
eq flip(privKey(N)) = pubKey(N) .

ceq dec(enc(P, KPub), KPriv) = P if KPriv == flip(KPub) .
eq dec(P, KPriv) = error("Mismatched private key OR unencrypted payload") [owise] .

ceq verify(sign(P, KPriv), KPub) = P if KPub == flip(KPriv) .
eq verify(P, KPub) = error("Mismatched signing key or unencrypted payload") [owise] .

endm

mod FIELD is
pr CRYPTO_ANCESTOR .

--- Sort for an exponent or a generator
sorts Gen Exp GenORExp .
subsorts Gen Exp < GenORExp .

--- Field generator G
op gen : -> Gen .

--- Allow an exponent to be a key or payload
subsort Exp < SymKey .
subsort Exp < Payload .

--- Represents a product of two nonces
sort NonceMult .
subsort Nonce < NonceMult .

op _*_ : NonceMult NonceMult -> NonceMult [assoc comm] .
op exp : GenORExp NonceMult -> Exp .

vars G : Gen .
vars NS NS' : NonceMult .
--- Used for handshakes to establish shared secret
eq exp(exp(G, NS), NS') = exp(G, NS * NS') .

endm

mod CRYPTO is

inc SYMMETRIC .
inc ASYMMETRIC .
inc FIELD .

--- standard mac and hash
op mac : SymKey Payload -> Payload [ctor] .
op hash : Nat Payload -> Payload [ctor] .

subsort String < Payload .
subsort Payload < SymKey .

endm


--- Crypto with object id as identity
mod CRYPTO_OBJECTS is 
pr CONFIGURATION .
inc CRYPTO .

vars O : Oid .

--- Example for defining custom priv / pub key types
--- Here we allow an object id to represent a key
op privKey : Oid -> PrivKey [ctor] .
op pubKey : Oid -> PubKey [ctor] .

eq flip(pubKey(O)) = privKey(O) .
eq flip(privKey(O)) = pubKey(O) .

op nonce : Oid Nat -> Nonce [ctor] .
op none : -> Nonce [ctor] .

endm